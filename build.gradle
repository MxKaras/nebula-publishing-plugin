buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-bintray-plugin:2.2.1'
    }
}

plugins {
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id 'nebula.contacts' version '2.2.0'
    id 'nebula.nebula-release' version '2.2.6'
    id 'nebula.optional-base' version '2.2.2'
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'com.github.kt3k.coveralls' version '2.4.0'
}

apply plugin: 'nebula.nebula-bintray'

description 'Gradle plugin to ease use of ivy-publish and maven-publish'
group = 'com.netflix.nebula'

contacts {
    'justin@halfempty.org' {
        moniker 'Justin Ryan'
        github 'quidryan'
    }
    'rob.spieldenner@gmail.com' {
        moniker 'Rob Spieldenner'
        github 'rspieldenner'
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'com.netflix.nebula:nebula-core:2.2.0'
    compile 'com.netflix.nebula:gradle-info-plugin:2.2.2', optional
    compile 'com.netflix.nebula:gradle-contacts-plugin:2.2.0', optional
    testCompile('com.netflix.nebula:nebula-test:2.2.2') {
        exclude group: 'org.codehaus.groovy'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/nebula-publishing-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/nebula-publishing-plugin.git'
    description = 'Plugins to ease maven-publish and ivy-publish configuration'

    plugins {
        /*publishing {
            id = 'nebula.publishing'
            displayName = 'Nebula Publshing plugin'
            tags = ['nebula', 'publishing', 'maven']
        }

        ivyPublishing {
            id = 'nebula.ivy-publishing'
            displayName = 'Nebula Ivy Publishing plugin'
            tags = ['nebula', 'publishing', 'ivy']
        }*/

        apacheLicensePom {
            id = 'nebula.apache-license-pom'
            displayName = 'Nebula Apache License Pom Plugin'
            description = 'Add the Apache v2 license to the pom'
            tags = ['nebula', 'publishing', 'maven', 'apache', 'license']
        }

        manifestPom {
            id = 'nebula.manifest-pom'
            displayName = 'Nebula Manifest Pom Plugin'
            description = 'Append additional information from our info plugin to the properties section of the pom'
            tags = ['nebula', 'publishing', 'maven']
        }

        mavenBasePublishing {
            id = 'nebula.maven-base-publishing'
            displayName = 'Nebula Maven Base Publishing Plugin'
            description = 'Setup basic nebula MavenPublication'
            tags = ['nebula', 'publishing', 'maven']
        }

        mavenJavaPublishing {
            id = 'nebula.maven-java-publishing'
            displayName = 'Nebula Maven Java Publishing Plugin'
            description = 'Add component.war if a war project, add component.java otherwise'
            tags = ['nebula', 'publishing', 'maven', 'java']
        }

        mavenPublishing {
            id = 'nebula.maven-publishing'
            displayName = 'Nebula Maven Publishing Plugin'
            description = 'Applies our opinions for maven publications'
            tags = ['nebula', 'publishing', 'maven']
        }

        resolvedPom {
            id = 'nebula.resolved-pom'
            displayName = 'Nebula Resolved Pom Plugin'
            description = 'Replaces dynamic versions with the actual resolved versions'
            tags = ['nebula', 'publishing', 'maven']
        }

        scmPom {
            id = 'nebula.scm-pom'
            displayName = 'Nebula SCM Pom Plugin'
            description = 'Adds SCM info to the pom'
            tags = ['nebula', 'publishing', 'maven']
        }

        javadocJar {
            id = 'nebula.javadoc-jar'
            displayName = 'Nebula Javadoc Jar Publishing plugin'
            description = 'Create a javadocJar task to package up the javadoc and add it to the publications'
            tags = ['nebula', 'publishing', 'javadoc']
        }

        sourceJar {
            id = 'nebula.source-jar'
            displayName = 'Nebula Source Jar Publishing plugin'
            description = 'Create a sourceJar task to package up the project\'s source code and add it to the publications'
            tags = ['nebula', 'publishing', 'source']
        }

        testJar {
            id = 'nebula.test-jar'
            displayName = 'Nebula Test Jar Publishing plugin'
            description = 'Create a testJar task to package up test classes and add it to the publications'
            tags = ['nebula', 'publishing', 'test']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.nebula'
        artifactId = 'nebula-publishing-plugin'
    }
}

task sourceJar(type: Jar) {
    dependsOn tasks.classes
    from sourceSets.main.allSource
    classifier 'sources'
    extension 'jar'
    group 'build'
}

task javadocJar(type: Jar) {
    dependsOn tasks.javadoc
    from tasks.javadoc.destinationDir
    classifier 'javadoc'
    extension 'jar'
    group 'build'
}

task groovydocJar(type: Jar) {
    dependsOn tasks.groovydoc
    from tasks.groovydoc.destinationDir
    classifier 'groovydoc'
    extension 'jar'
    group 'build'
}

publishing {
    publications {
        mavenNebula(MavenPublication) {
            from components.java
            artifact tasks.sourceJar
            artifact tasks.javadocJar
            artifact tasks.groovydocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                def pomProperties = root.appendNode('properties')
                pomProperties.appendNode('nebula_Manifest_Version', '1.0')
                pomProperties.appendNode('nebula_Implementation_Title', "${project.group}:${project.name}:${project.version}")
                pomProperties.appendNode('nebula_Implementation_Version', project.version)
                pomProperties.appendNode('nebula_Build_Date', new Date().format('yyyy-MM-dd_HH:mm:ss'))
                pomProperties.appendNode('nebula_Created_By', "${System.getProperty('java.runtime.version')} (${System.getProperty('java.vm.specification.vendor')})")
                pomProperties.appendNode('nebula_Build_Java_Version', System.getProperty('java.version'))
                pomProperties.appendNode('nebula_X_Compile_Target_JDK', project.targetCompatibility)
                pomProperties.appendNode('nebula_X_Compile_Source_JDK', project.sourceCompatibility)
                root.appendNode('url', 'https://github.com/nebula-plugins/nebula-publishing-plugin')
                root.appendNode('scm').appendNode('url', 'https://github.com/nebula-plugins/nebula-publishing-plugin.git')
                def devs = root.appendNode('developers')
                def rob = devs.appendNode('developer')
                rob.appendNode('id', 'rspieldenner')
                rob.appendNode('name', 'Rob Spieldenner')
                rob.appendNode('email', 'rob.spieldenner@gmail.com')
                def justin = devs.appendNode('developer')
                justin.appendNode('id', 'quidryan')
                justin.appendNode('name', 'Justin Ryan')
                justin.appendNode('email', 'justin@halfempty.org')
                def apache = root.appendNode('licenses').appendNode('license')
                apache.appendNode('name', 'The Apache Software License, Version 2.0')
                apache.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                apache.appendNode('distribution', 'repo')
            }
        }
    }
}

// Wire up build and publishing process
project.tasks.getByName('verifyReleaseStatus').actions.clear()
project.tasks.getByName('verifySnapshotStatus').actions.clear()

tasks.matching { it.name == 'bintrayUpload' || it.name == 'artifactoryPublish'}.all { Task task ->
    task.mustRunAfter('build')
    tasks.release.dependsOn(task)
}

tasks.matching { it.name == 'bintrayUpload' }.all { Task task ->
    project.gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
        task.onlyIf {
            graph.hasTask(':final') || graph.hasTask(':candidate')
        }
    }
}

project.tasks.matching { it.name == 'artifactoryPublish'}.all { Task task ->
    project.gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
        task.onlyIf {
            graph.hasTask(':snapshot')
        }
    }
}

import org.ajoberstar.gradle.git.release.base.ReleasePluginExtension
ReleasePluginExtension releaseExtension = project.extensions.findByType(ReleasePluginExtension)
releaseExtension.with {
    defaultVersionStrategy = nebula.plugin.release.NetflixOssStrategies.SNAPSHOT
}

if (project.hasProperty('release.travisci') && project.property('release.travisci').toBoolean()) {
    project.tasks.release.deleteAllActions()
    project.tasks.prepare.deleteAllActions()
    nebulaRelease {
        addReleaseBranchPattern(/HEAD/)
        addReleaseBranchPattern(/v?\d+\.\d+\.\d+/)
        addReleaseBranchPattern(/gradle-\d+\.\d+/)
    }
}
