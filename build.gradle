buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.0'
    }
}

plugins {
    id 'nebula.contacts' version '2.2.0'
    id 'groovy'
    id 'idea'
}

description 'Gradle plugin to publish to bintray'
apply plugin: 'com.gradle.plugin-publish'

contacts {
    'justin@halfempty.org' {
        moniker 'Justin Ryan'
        github 'quidryan'
    }
    'rob.spieldenner@gmail.com' {
        moniker 'Rob Spieldenner'
        github 'rspieldenner'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'com.netflix.nebula:nebula-core:2.2.+'
    compile 'com.netflix.nebula:gradle-info-plugin:2.2.+'
    compile 'com.netflix.nebula:gradle-contacts-plugin:2.2.+', optional
    testCompile('com.netflix.nebula:nebula-test:2.2.+') {
        exclude group: 'org.codehaus.groovy'
    }
}

task showBuildscript << {
   buildscript.configurations.classpath.each { println it }
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/nebula-publishing-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/nebula-publishing-plugin.git'
    description = 'Plugins to ease maven-publish and ivy-publish configuration'

    plugins {
        publishing {
            id = 'nebula.publishing'
            displayName = 'Nebula Publshing plugin'
            tags = ['nebula', 'publishing']
        }

        ivyPublishing {
            id = 'nebula.ivy-publishing'
            displayName = 'Nebula Ivy Publishing plugin'
            tags = ['nebula', 'publishing', 'ivy']
        }

        javadocJar {
            id = 'nebula.javadoc-jar'
            displayName = 'Nebula Javadoc Jar Publishing plugin'
            tags = ['nebula', 'publishing', 'javadoc']
        }

        sourceJar {
            id = 'nebula.source-jar'
            displayName = 'Nebula Source Jar Publishing plugin'
            tags = ['nebula', 'publishing', 'source']
        }

        testJar {
            id = 'nebula.test-jar'
            displayName = 'Nebula Test Jar Publishing plugin'
            tags = ['nebula', 'publishing', 'test']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.nebula'
        artifactId = 'nebula-publishing-plugin'
    }
}
